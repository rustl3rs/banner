pipeline_definition = {
    SOI ~(task_definition | import_declaration)* ~ EOI
}

task_definition = {
    (tag_definition)* ~ "task" ~ identifier ~ parameter_list ~ block
}

parameter_list = _{
    "(" ~ "image" ~ ":" ~ image_identifier ~ "," ~ "execute" ~ ":" ~ string_literal ~ ")"
}

block = _{
    "{" ~ raw_string ~ "}"
}

identifier = { (ASCII_ALPHANUMERIC | "_" | "-")+ }
image_identifier = { (ASCII_ALPHANUMERIC | "_" | "-" | "/" | ":")+ }
string_literal = { ("\"" ~ identifier ~ "\"" | raw_string){1} }
string_content = { identifier* }

// raw strings was taken directly from 
// https://pest.rs/book/grammars/syntax.html#the-stack-wip
raw_string = _{
    "r" ~ PUSH("#"*) ~ "\""    // push the number signs onto the stack
    ~ raw_string_interior
    ~ "\"" ~ POP               // match a quotation mark and the number signs
}

raw_string_interior = {
    (
        !("\"" ~ PEEK)    // unless the next character is a quotation mark
                          // followed by the correct amount of number signs,
        ~ ANY             // consume one character
    )*
}

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

// like: [tag: rustl3rs.com/team=platform-team]
tag_definition = _{ "[" ~ "tag" ~ ":" ~ tag ~ "]" }
tag = { tag_key ~ "=" ~ tag_value }
tag_key = { (ASCII_ALPHANUMERIC | "_" | "-" | "/" | ".")+ }
tag_value = { (ASCII_ALPHANUMERIC | "_" | "-" | "/" | ".")+ }

import_declaration = ${ "import" ~ WHITESPACE ~ URI  ~ NEWLINE }
// URI parsing ðŸ˜­ðŸ¤• https://www.rfc-editor.org/rfc/rfc3986#appendix-A
// Accept basically anything as a URI on the way in, make any transformations
// that are required because of the use of variables, and then parse it a touch 
// later with the iref crate.
URI = ${ scheme ~ ":" ~ "//" ~ heir_query_fragment }
scheme = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "+" | "-" | ".")* }
heir_query_fragment = @{ (ASCII_ALPHA | ASCII_DIGIT | "-" | "." | "_" | "~" | ":" | "/" | "?" | "#" | "[" | "]" | "@" | "!" | "$" | "&" | "'" | "(" | ")" | "*" | "+" | "," | ";" | "=")+ }
